buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
        classpath 'io.fabric.tools:gradle:1.31.2'
    }
}

def versionMajor = 6
def versionMinor = 7
def versionPatch = 1
def versionBuild = 1

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply plugin: 'hugo'
apply plugin: 'com.github.ben-manes.versions'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
    maven { url "https://clojars.org/repo/" }
}

def gitSha = { ->
    def p = 'git rev-parse --short HEAD'.execute([], project.rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        throw new RuntimeException(p.errorStream.text)
    }

    return p.text.trim()
}

def gitBranchName = { ->
    def p = 'git rev-parse --abbrev-ref HEAD'.execute([], project.rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        throw new RuntimeException(p.errorStream.text)
    }

    return p.text.trim()
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    lintOptions {
        baseline file("lint-baseline.xml")
        checkAllWarnings true
        warningsAsErrors true
        abortOnError true
        checkReleaseBuilds false
    }

    dexOptions {
        jumboMode true
    }

    defaultConfig {
        applicationId "com.boardgamegeek"

        minSdkVersion 16
        targetSdkVersion 28
        multiDexEnabled true

        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        def buildTime = new GregorianCalendar().format("yyyy-MM-dd h:mm:ss a z")
        buildConfigField 'String', 'BUILD_TIME', "\"${buildTime}\""
        buildConfigField 'String', 'GIT_SHA', "\"${gitSha()}\""
        buildConfigField 'String', 'GIT_BRANCH', "\"${gitBranchName()}\""

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [eventBusIndex: 'com.boardgamegeek.events.BggEventBusIndex']
            }
        }
    }

    buildTypes {
        applicationVariants.all { variant ->
            variant.outputs.all { output ->
                def branchName = gitBranchName().replace('/', "-")
                if (branchName == "master") branchName = ""
                outputFileName = output.outputFile.name.replace(".apk", "-" + defaultConfig.versionName + "_" + branchName + ".apk")
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (gitBranchName() == "master") {
                versionNameSuffix ""
            } else if (gitBranchName().startsWith("feature")) {
                versionNameSuffix " (${gitBranchName()})"
            } else {
                versionNameSuffix " RC${versionBuild}"
            }
        }
        debug {
            if (versionBuild > 0) {
                versionNameSuffix " build ${versionBuild} (${gitBranchName()})"
            } else {
                versionNameSuffix " (${gitBranchName()})"
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'androidx.core:core-ktx:1.1.0'

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.1.0-beta01'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha4'
    implementation 'androidx.preference:preference:1.1.0'

    implementation 'androidx.multidex:multidex:2.0.1'

    implementation "org.jetbrains.anko:anko-support-v4-commons:$anko_version"
    implementation "org.jetbrains.anko:anko-commons:$anko_version"
    implementation "org.jetbrains.anko:anko-design:$anko_version"

    implementation 'androidx.lifecycle:lifecycle-viewmodel:2.1.0'
    implementation 'androidx.lifecycle:lifecycle-livedata:2.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'

    implementation 'com.google.firebase:firebase-core:17.2.0'
    implementation 'com.google.firebase:firebase-config:19.0.3'
    implementation 'com.google.firebase:firebase-messaging:20.0.0'

    implementation 'com.google.code.gson:gson:2.8.6'

    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation("com.squareup.retrofit2:converter-simplexml:$retrofit_version") {
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
        exclude group: 'xpp3', module: 'xpp3'
    }

    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    implementation 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'

    implementation 'com.squareup.picasso:picasso:2.5.2'

    implementation "com.jakewharton:butterknife:$butterknife_version"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknife_version"

    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    implementation 'com.github.rhlff:NestedScrollWebView:v1.0.2'

    implementation('com.crashlytics.sdk.android:crashlytics:2.10.1@aar') {
        transitive = true
    }

    implementation('com.mikepenz:aboutlibraries:7.0.3@aar') {
        transitive = true
    }

    implementation 'org.greenrobot:eventbus:3.1.1'
    annotationProcessor 'org.greenrobot:eventbus-annotation-processor:3.1.1'

    implementation 'com.github.PhilJay:MPAndroidChart:3.1.0'

    implementation 'com.appyvet:materialrangebar:1.4.6'

    implementation "com.facebook.stetho:stetho:$stetho_version"
    implementation "com.facebook.stetho:stetho-okhttp3:$stetho_version"

    implementation 'com.github.amlcurran.showcaseview:library:5.4.3'

    implementation 'org.jsoup:jsoup:1.12.1'

    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'io.reactivex:rxjava:1.3.8'

    implementation 'frankiesardo:icepick:3.2.0'
    compileOnly 'frankiesardo:icepick-processor:3.2.0'
    annotationProcessor 'frankiesardo:icepick-processor:3.2.0'

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"
}

apply plugin: 'com.google.gms.google-services'
