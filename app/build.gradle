buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.8.1'
    }
}

def versionMajor = 6
def versionMinor = 8
def versionPatch = 2
def versionBuild = 1

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'kotlin-parcelize'

repositories {
    maven { url "https://jitpack.io" }
    maven { url "https://clojars.org/repo/" }
}

def gitSha = { ->
    def p = 'git rev-parse --short HEAD'.execute([], project.rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        throw new RuntimeException(p.errorStream.text)
    }

    return p.text.trim()
}

def gitBranchName = { ->
    def p = 'git rev-parse --abbrev-ref HEAD'.execute([], project.rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        throw new RuntimeException(p.errorStream.text)
    }

    return p.text.trim()
}

android {
    compileSdkVersion 31

    buildFeatures {
        viewBinding true
    }


    defaultConfig {
        applicationId "com.boardgamegeek"

        minSdkVersion 19
        targetSdkVersion 31
        multiDexEnabled true

        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        def buildTime = new GregorianCalendar().format("yyyy-MM-dd h:mm:ss a z")
        buildConfigField 'String', 'BUILD_TIME', "\"${buildTime}\""
        buildConfigField 'String', 'GIT_SHA', "\"${gitSha()}\""
        buildConfigField 'String', 'GIT_BRANCH', "\"${gitBranchName()}\""

        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        applicationVariants.all { variant ->
            variant.outputs.all { output ->
                def branchName = gitBranchName().replace('/', "-")
                if (branchName == "master") branchName = ""
                outputFileName = output.outputFile.name.replace(".apk", "-" + defaultConfig.versionName + "_" + branchName + ".apk")
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (gitBranchName() == "master") {
                versionNameSuffix ""
            } else if (gitBranchName().startsWith("feature")) {
                versionNameSuffix " (${gitBranchName()})"
            } else {
                versionNameSuffix " RC${versionBuild}"
            }
        }
        debug {
            if (versionBuild > 0) {
                versionNameSuffix " build ${versionBuild} (${gitBranchName()})"
            } else {
                versionNameSuffix " (${gitBranchName()})"
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
    lint {
        abortOnError true
        baseline file('lint-baseline.xml')
        checkAllWarnings true
        checkReleaseBuilds false
        warningsAsErrors true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.0"

    implementation 'androidx.core:core-ktx:1.7.0'

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.fragment:fragment-ktx:1.4.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.annotation:annotation:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    def paging_version = "3.1.1"
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"

    implementation 'androidx.multidex:multidex:2.0.1'

    def arch_lifecycle_version = '2.4.1'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$arch_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$arch_lifecycle_version"

    implementation platform('com.google.firebase:firebase-bom:29.0.4')
    implementation 'com.google.firebase:firebase-core'
    implementation 'com.google.firebase:firebase-config'
    implementation 'com.google.firebase:firebase-config-ktx'
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    implementation 'com.google.code.gson:gson:2.8.9'

    def retrofit_version = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation("com.squareup.retrofit2:converter-simplexml:$retrofit_version") {
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
        exclude group: 'xpp3', module: 'xpp3'
    }

    def okhttp_version = '4.9.3'
    //noinspection GradleDependency
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    //noinspection GradleDependency
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    implementation 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'

    //noinspection GradleDependency
    implementation 'com.squareup.picasso:picasso:2.5.2'

    implementation 'com.jakewharton.timber:timber:5.0.1'

    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    implementation('com.mikepenz:aboutlibraries:7.1.0@aar') {
        transitive = true
    }

    implementation 'com.github.PhilJay:MPAndroidChart:3.1.0'

    def stetho_version = '1.6.0'
    implementation "com.facebook.stetho:stetho:$stetho_version"
    implementation "com.facebook.stetho:stetho-okhttp3:$stetho_version"

    implementation 'org.jsoup:jsoup:1.14.3'

    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'io.reactivex:rxjava:1.3.8'

    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.8.1"
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
