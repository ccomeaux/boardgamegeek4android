buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.7'
    }
}

plugins {
    id 'com.google.dagger.hilt.android'
    id 'kotlin-kapt'
}

def versionMajor = 6
def versionMinor = 8
def versionPatch = 2
def versionBuild = 1

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'kotlin-parcelize'

repositories {
    maven { url "https://jitpack.io" }
    maven { url "https://clojars.org/repo/" }
}

android {
    namespace 'com.boardgamegeek'
    compileSdk 34


    buildFeatures {
        viewBinding true
        buildConfig true
    }


    defaultConfig {
        applicationId "com.boardgamegeek"

        minSdkVersion 19
        targetSdk 34
        multiDexEnabled true

        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        def buildTime = new GregorianCalendar().format("yyyy-MM-dd h:mm:ss a z")
        buildConfigField 'String', 'BUILD_TIME', "\"${buildTime}\""

        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
    lint {
        abortOnError true
        baseline file('lint-baseline.xml')
        checkAllWarnings true
        checkReleaseBuilds false
        warningsAsErrors true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    def hilt_version = '2.47'
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3"

    implementation 'androidx.core:core-ktx:1.10.1'

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.activity:activity-ktx:1.7.2'
    implementation 'androidx.fragment:fragment-ktx:1.6.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.1'
    implementation 'androidx.annotation:annotation:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation "androidx.paging:paging-runtime-ktx:3.2.0"

    implementation 'androidx.work:work-runtime-ktx:2.8.1'
    implementation 'androidx.hilt:hilt-work:1.0.0'
    kapt 'androidx.hilt:hilt-compiler:1.0.0'

    implementation 'androidx.multidex:multidex:2.0.1'

    def arch_lifecycle_version = '2.6.1'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$arch_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$arch_lifecycle_version"

    implementation platform('com.google.firebase:firebase-bom:32.2.0')
    implementation 'com.google.firebase:firebase-config-ktx'
    implementation 'com.google.firebase:firebase-messaging-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    implementation 'com.google.code.gson:gson:2.10.1'

    def retrofit_version = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation("com.squareup.retrofit2:converter-simplexml:$retrofit_version") {
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
        exclude group: 'xpp3', module: 'xpp3'
    }

    def okhttp_version = '4.10.0'
    //noinspection GradleDependency
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    //noinspection GradleDependency
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    implementation 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'

    //noinspection GradleDependency
    implementation 'com.squareup.picasso:picasso:2.5.2' // 2.8?

    implementation 'com.jakewharton.timber:timber:5.0.1'

    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    implementation('com.mikepenz:aboutlibraries:7.1.0@aar') {
        transitive = true
    }

    implementation 'com.github.PhilJay:MPAndroidChart:3.1.0'

    def stetho_version = '1.6.0'
    implementation "com.facebook.stetho:stetho:$stetho_version"
    implementation "com.facebook.stetho:stetho-okhttp3:$stetho_version"

    implementation 'org.jsoup:jsoup:1.16.1'

    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.12"
}

kapt {
    correctErrorTypes true
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
