buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
        classpath 'com.google.gms:google-services:4.4.2'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:3.0.2'
    }
}

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.firebase.crashlytics'
    id 'com.google.gms.google-services'
    //id 'com.github.ben-manes.versions' version '0.53.0'
}

def versionMajor = 6
def versionMinor = 8
def versionPatch = 3
def versionBuild = 3

def gitSha = { ->
    def p = 'git rev-parse --short HEAD'.execute([], project.rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        throw new RuntimeException(p.errorStream.text)
    }

    return p.text.trim()
}

def gitBranchName = { ->
    def p = 'git rev-parse --abbrev-ref HEAD'.execute([], project.rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        throw new RuntimeException(p.errorStream.text)
    }

    return p.text.trim()
}

android {
    namespace "com.boardgamegeek"
    compileSdkVersion 36

    lintOptions {
        baseline file("lint-baseline.xml")
        checkAllWarnings true
        warningsAsErrors true
        abortOnError true
        checkReleaseBuilds false
    }

    defaultConfig {
        applicationId "com.boardgamegeek"

        minSdk 23
        targetSdk 35
        multiDexEnabled true

        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        def buildTime = new GregorianCalendar().format("yyyy-MM-dd h:mm:ss a z")
        buildConfigField 'String', 'BUILD_TIME', "\"${buildTime}\""
        buildConfigField 'String', 'GIT_SHA', "\"${gitSha()}\""
        buildConfigField 'String', 'GIT_BRANCH', "\"${gitBranchName()}\""

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [eventBusIndex: 'com.boardgamegeek.events.BggEventBusIndex']
            }
        }
    }

    buildTypes {
        applicationVariants.all { variant ->
            variant.outputs.all { output ->
                def branchName = gitBranchName().replace('/', "-")
                if (branchName == "master") branchName = ""
                outputFileName = output.outputFile.name.replace(".apk", "-" + defaultConfig.versionName + "_" + branchName + ".apk")
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (gitBranchName() == "master") {
                versionNameSuffix ""
            } else if (gitBranchName().startsWith("feature")) {
                versionNameSuffix " (${gitBranchName()})"
            } else {
                versionNameSuffix " RC${versionBuild}"
            }
        }
        debug {
            if (versionBuild > 0) {
                versionNameSuffix " build ${versionBuild} (${gitBranchName()})"
            } else {
                versionNameSuffix " (${gitBranchName()})"
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation 'androidx.core:core-ktx:1.17.0'

    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.annotation:annotation:1.9.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.preference:preference-ktx:1.2.1'

    implementation 'androidx.multidex:multidex:2.0.1'

    implementation "com.github.zengzzGithub:AnkoX:1.0.4"

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"

    // Firebase (Updated to latest BOM approach recommended)
    implementation platform('com.google.firebase:firebase-bom:34.5.0')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-config'
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-crashlytics'

    implementation 'com.google.code.gson:gson:2.13.2'

    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation("com.squareup.retrofit2:converter-simplexml:$retrofit_version") {
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
        exclude group: 'xpp3', module: 'xpp3'
    }

    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    implementation 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'

    implementation 'com.squareup.picasso:picasso:2.5.2'

    implementation 'com.jakewharton.timber:timber:5.0.1'

    implementation 'com.github.chrisbanes:PhotoView:2.3.0'


    implementation('com.mikepenz:aboutlibraries:7.0.3@aar') {
        transitive = true
    }

    implementation 'org.greenrobot:eventbus:3.3.1'
    annotationProcessor 'org.greenrobot:eventbus-annotation-processor:3.3.1'

    implementation 'com.github.PhilJay:MPAndroidChart:3.1.0'

    implementation 'com.github.oli107:material-range-bar:1.4.8'

    implementation "com.facebook.stetho:stetho:$stetho_version"
    implementation "com.facebook.stetho:stetho-okhttp3:$stetho_version"

    implementation 'com.github.amlcurran.showcaseview:library:5.4.3'

    implementation 'org.jsoup:jsoup:1.21.2'

    // RxJava (Consider migrating to Kotlin Coroutines/Flow)
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'

    debugImplementation "com.squareup.leakcanary:leakcanary-android:2.14"

    annotationProcessor 'com.google.auto.service:auto-service:1.1.1'
}

apply plugin: 'com.google.gms.google-services'
